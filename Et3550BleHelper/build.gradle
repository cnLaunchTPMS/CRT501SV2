plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.cnlaunch.et3550blehelper'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            )
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    /**
     * 关键点 1：声明只发布 release 变体
     */
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

/**
 * 关键点 2：必须 afterEvaluate
 */
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.SwaggyBorn'
                artifactId = 'Et3550BleHelper'
                version = '1.0'

                from components.release
            }
        }
    }
}

dependencies {

    def lifecycle_version = "2.5.1"

    api 'androidx.appcompat:appcompat:1.6.1'
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    api "org.jetbrains.kotlin:kotlin-reflect:1.8.22"
    api "org.jetbrains.kotlinx:kotlinx-serialization-core:1.2.2"
    api 'cn.com.superLei:blelibrary:3.2.0'


    //TODO
    //发布到本地仓库命令
    //./gradlew :Et3550BleHelper:publishToMavenLocal
    //别人引入
    //implementation "com.github.SwaggyBorn:Et3550BleHelper:1.0"
}
